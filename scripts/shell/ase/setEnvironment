#!/bin/tcsh
#set echo
#set verbose

if ( $# > 0 ) then
  set BUILD_TYPE = $1
else
  set BUILD_TYPE = "algomake"
endif

switch ( $BUILD_TYPE )
case "run":
  if ( ${%2} != 0 ) then
    set BUILD_NUM = $2
  else
    set BUILD_NUM = "latest"
  endif

  if ( ${%3} != 0 ) then
    setenv ASE_VERSION $3
    set VERCOM = `/home/ase/TestSuite/scripts/compareReleases.pl "4.7.0" $ASE_VERSION`
    if ( $VERCOM < 0 ) then
      switch ( $ASE_VERSION )
      case "4.6.1":
        set USE_PLATFORM = "rhel-i686-5.2"
        set USE_COMPILER = "gcc-4.1.2"
        breaksw
      case "4.6.0":
        set USE_PLATFORM = "rhel-i686-4.0"
        set USE_COMPILER = "gcc-4.1.2"
        breaksw
      case "4.5.4":
        set USE_PLATFORM = "rhel-i686-4.0"
        set USE_COMPILER = "gcc-3.3.3"
        breaksw
      case "4.5.3":
        set USE_PLATFORM = "linux-i386-2.3"
        set USE_COMPILER = "gcc-3.3.3"
        breaksw
      endsw
    else
      set USE_PLATFORM = $PLATFORM_WIDE
      set USE_COMPILER = $MYCOMPILER
    endif
  else
    set USE_PLATFORM = $PLATFORM_WIDE
    set USE_COMPILER = $MYCOMPILER
  endif

  setenv BINDIR /mnts/run/ASE-${ASE_VERSION}/$BUILD_NUM/${USE_PLATFORM}-${USE_COMPILER}/ase-v${ASE_VERSION}/bin/
  breaksw
case "cmake":
  setenv BINDIR $TEMP/builds/ase480/ship/${PLATFORM_WIDE}/ase-v4.7.0/bin
  breaksw
case "algomake":
  if ( $ASE_VERSION == "4.7.0" ) then
    set USE_PLATFORM = ${PLATFORM_WIDE}
  else
    set USE_PLATFORM = ${PLATFORM}
  endif
  setenv BINDIR $SOURCE/ASE-${ASE_VERSION}/ship/${PLATFORM_WIDE}/ase-v/bin
  breaksw
default:
  echo "Usage: setEnvironment [build type] <build number> [version]"
  echo "Build type must be 'run','cmake' or 'algomake' (default)"
  exit (1)
  breaksw
endsw

if ( ! -d $BINDIR ) then
  echo "$BINDIR doesn't exist"
endif

source ~/ase/scripts/aseenv

#if ( $BUILD_TYPE == "algomake" && ${PLATFORM_WIDE} == "rhel-x86_64-5.2" ) then
#  setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:/mnts/cdstools/IntelCxx-11.0p74/rhel-x86_64-5.2/lib/intel64/:/mnts/cdstools/IntelMKL-10.3.4.191/rhel-x86_64-5.2/mkl/lib/intel64/
#  echo "Linux needs extra LD_LIBRARY_PATH: $LD_LIBRARY_PATH for NOSOURCEPRO"
#endif

